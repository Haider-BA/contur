C     MAIN PART OF
C     PROGRAM CONTUR(INPUT,OUTPUT,TAPE5=INPUT,TAPE6=OUTPUT)
C
C     NOZZLE CONTOUR PROGRAM VEV00028 FOR AXISYMMETRIC OR PLANAR FLOW
C     WITH RADIAL FLOW REGION AND/OR WITH CENTER-LINE VELOCITY OR MACH
C     NUMBER DISTRIBUTIONS DEFINED BY POLYNOMIALS.
C
C     CORRECTION APPLIED FOR GROWTH OF TURBULENT BOUNDARY LAYER.
C     PERFECT GAS IS ASSUMED WITH CONSTANT SPECIFIC HEAT RATIO, GAM,
C     COMPRESSIBILITY FACTOR, ZO, AND RECOVERY FACTOR, RO, AS INPUTS.
C     ALSO INPUT IS GAS CONSTANT, AR, IN SQ FT PER SQ SECOND PER DEG R.
C     IF VISM IS SUTHERLANDS TEMPERATURE, VISCOSITY FOLLOWS SUTHERLANDS
C     LAW ABOVE VISM, BUT IS LINEAR WITH TEMPERATURE BELOW VISM.
C     IF (VISM .LE. 1.D+0) VISCOSITY=VISC*TEMPERATURE**VISM MAI
C
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON /GG/ GAM,GM,G1,G2,G3,G4,G5,G6,G7,G8,G9,GA,RGA,QT
      COMMON /COORD/ S(200),FS(200),WALTAN(200),SD(200),WMN(200),TTR(200
     1),DMDX(200),SPR(200),DPX(200),SREF(200),XBIN,XCIN,GMA,GHB,GMC,GMD
      COMMON /CORR/ DLA(200),RCO(200),DAX(200),DRX(200),SL(200),DR2
      COMMON /PROP/ AR,ZO,RO,VISC,VISM,SFOA,XBL,CONV
      COMMON /PARAM/ ETAD,RC,AMACH,BMACH,CMACH,EMACH,GMACH,FRC,SF,WWO,WW
     1OP,QM,WE,CBET,XE,ETA,EPSI,BPSI,XO,YO,RRC,SDO,XB,XC,AH,PP,SE,TYE,XA
      COMMON /JACK/ SJ(30),XJ(30),YJ(30),AJ(30)
      COMMON /CONTR/ ITLE(3),IE,LR,IT,JB,JQ,JX,KAT,KBL,KING,KO,LV,NOCON
      DATA ZRO/0.0D+0/,ONE/1.D+0/,TWO/2.D+0/,DC7/8HCURVATUR/
      DATA DC1/8H D2Y/DX2/,DC2/8H        /,DC3/8H   ANGLE/
      DATA DC4/8H   DY/DX/,DC5/8H   DY/DS/,DC6/8H   DX/DS/
      DATA L1/4H   X/,L2/4H   Y/,L3/4H   S/,L4/4H    /,L5/4HDIFF/
      CONV=90.D+0/DASIN(ONE)
      IT=0
      NC=0
      LA=L1
      LB=L4
      DCA=DC4
      DCB=DC2
      JJ=1000
      DCC=DC1
C
      OPEN(1,FILE='input.txt',STATUS='OLD',ACCESS='SEQUENTIAL',
     1FORM='FORMATTED',ACTION='READ',BLANK='NULL')
      OPEN(UNIT=2, FILE='data.txt', STATUS='OLD')
1     READ (1,30,END=24) ITLE,JD
      IF (ITLE(1) .EQ. L4) GO TO 24
      IE=1+JD
      QT=ONE/(1+IE)
C
      READ (1,28) GAM,AR,ZO,RO,VISC,VISM,SFOA,XBL
C     FOR GAMMA=1.4, G9=5, G8=.2, G7=1.2, G6=5/6, G5=1/6, G4=1/SQRT(6),
C     G3=1.5, G2=SQRT(6), G1=2.5
      GM=GAM-ONE
      G1=ONE/GM
      G9=TWO*G1
      G8=ONE/G9
      G7=ONE+G8
      G6=ONE/G7
      G5=G8*G6
      RGA=TWO*G5
      GA=ONE/RGA
      G4=DSQRT(G5)
      G3=GA/TWO
      G2=ONE/G4
      IF (IE .EQ. 0) AH=ZO
      IF (IE .EQ. 0) ZO=ONE
      QM=ONE
      JX=0
2     JQ=0
      LV=0
3     CALL AXIAL
      IF (LV .LT. 0) GO TO 1
      CALL PERFC
      IF (NOCON .NE. 0) GO TO 24
      IF ((JQ .GT. 0) .OR. (JX .GT. 0)) GO TO 3
      IF (JB .GT. 0) CALL BOUND
      IF (XBL .EQ. 1.D+3) GO TO 5
      IF (IT .LT. 1) GO TO 4
      LA=L3
      DCA=DC5
      DCC=DC7
      KUP=IT
      KAP=KUP+1
      XEND=ZRO
C
      READ (1,28,END=24) (SJ(K),K=1,KUP),XST
      CSK=ONE/DSQRT(ONE+DRX(KAT)**2)
      SNK=CSK*DRX(KAT)
      CALL SPLIND (SL,RCO,ZRO,SNK,KAT)
      GO TO 6
4     IF (LV .GT. 0) GO TO 24
      IF (JX .LT. 0) GO TO 1
      GO TO 2
5     CONTINUE
C
      READ (1,28,END=24) XST,XLOW,XEND,XINC,BJ,XMID,XINC2,CN
      IF (XST .EQ. XBL) XST=S(1)
      NC=CN-ONE
      IF (JB .LE. 0) CALL SPLIND (S,FS,WALTAN(1),WALTAN(KING),KING)
      IF (JB .GT. 0) CALL SPLIND (S,RCO,DRX(1),DRX(KAT),KAT)
      IF (XEND .GT. ZRO) GO TO 6
      LB=L5
      DCB=DC4
6     SLONG=S(KING)-S(1)
      IPP=0
      WRITE (2,25) ITLE,SLONG
      WRITE (2,31) LA,L2,DCA,DC3,DCC,DCB,LB
      IF (JB .GT. 0) GO TO 7
      WRITE (2,26) XST,FS(1),WALTAN(1),ZRO,SD(1)
      XMAX=SLONG+XST
      YMAX=FS(KING)
      TMAX=WALTAN(KING)
      GO TO 8
7     WRITE (2,26) XST,RCO(1),DRX(1),ZRO,DR2
      XMAX=S(KAT)-S(1)+XST
      YMAX=RCO(KAT)
      TMAX=DRX(KAT)
8     IF (IT .GT. 0) GO TO 11
      JB=BJ
      IF (XEND .GT. ZRO) GO TO 10
      IF (JB .LT. 0) GO TO 9
      KUP=KING-1
      KAP=KING-1
      GO TO 11
9     KUP=-JB
      KAP=KUP+1
C
      READ (1,28,END=24) (SJ(K),K=1,KUP)
      GO TO 11
10    IF (XINC .GT. ZRO) KUP=(XEND-XLOW)/XINC+1.D-2
      IF (XMID .NE. ZRO) JJ=(XMID-XLOW)/XINC+1.D-2
      IF (XMID .NE. ZRO) KUP=JJ+(XEND-XMID)/XINC2+1.D-2
      IF (JB .GT. 10) KUP=JB
      IF (JB .GT. 10) XINC=SLONG/BJ
      KAP=(XMAX-XLOW)/XINC+1
      IF (XMID .NE. ZRO) KAP=JJ+(XMAX-XMID)/XINC2+1
11    DO 19 K=1,KUP
      IF (XEND .EQ. ZRO) GO TO 12
      X=XLOW+K*XINC
      IF (K .GT. JJ) X=XMID+(K-JJ)*XINC2
      GO TO 13
12    IF ((IT .LT. 1) .AND. (JB .GE.0)) X=S(K+1)
      IF ((IT .GT. 0) .OR. (JB .LT.0)) X=SJ(K)
13    XX=X-XST+S(1)
      IF (K .LT. KAP) CALL XYZ (XX,YY,YYP,YYPP)
      IF (K .EQ. KAP) X=XMAX
      IF (K .GE. KAP) YY=YMAX
      IF (K .GE. KAP) YYP=TMAX
      IF (K .GE. KAP) YYPP=ZRO
      IF (IT .LT. 1) GO TO 16
      IF (IPP .GT. 0) GO TO 14
      YJ(K)=YY
      AJ(K)=DASIN(YYP)
      WANG=CONV*AJ(K)
      CURV=YYPP/DCOS(AJ(K))
      WRITE (2,26) X,YY,YYP,WANG,CURV
      GO TO 18
14    YY=YY-S(1)+XST
      XJ(K)=YY
      WANG=CONV*DACOS(YYP)
      WRITE (2,26) X,YY,YYP,WANG
      GO TO 18
16    WANG=CONV*DATAN(YYP)
      IF ((XEND .EQ. ZRO) .AND. (JB .GE. 0)) DY=YYP-WALTAN(K+1)
      IF (JB .LE. 0) GO TO 17
      FS(K+1)=YY
      WALTAN(K+1)=YYP
      SD(K+1)=YYPP
17    IF (XEND.GT.ZRO.OR.JB.LT.0) WRITE (2,26) X,YY,YYP,WANG,YYPP
      IF (XEND.EQ.ZRO.AND.JB.GE.0) WRITE (2,26) X,YY,YYP,WANG,YYPP,DY
18    IF (MOD(K,10) .EQ. 0) WRITE (2,29)
      IF (MOD(K,50) .NE. 0) GO TO 19
      WRITE (2,25) ITLE,SLONG
      WRITE (2,31) LA, L2, DCA, DC3, DCC, DCB, LB
19    CONTINUE
      IF ((IT .GT. 0) .AND. (IPP .EQ. 1)) CALL PLATE
      IF (IPP .GE. NC) GO TO 20
      IPP= IPP+1
      WRITE (2,25) ITLE, SLONG
      WRITE (2,31) LA,L2,DCA,DC3,DCC
      WRITE (2,26) XST,RCO(1),DRX(1),ZRO,DR2
      GO TO 11
20    IF ((IPP .GT. 0) .OR. (JX .LT. 0)) GO TO 1
      IF (IT .EQ. 0) GO TO 21
      IPP=1
      CALL SPLIND (SL,S,ONE,CSK,KAT)
      WRITE (2,29)
      WRITE (2,31) L3,L1,DC6,DC3
      WRITE (2,26) XST,XST,ONE,ZRO
      GO TO 11
21    IF (JB) 1,2,22
22    CALL SPLIND (S,WMN,DMDX(1),DMDX(KING),KING)
      DO 23 K=1,KUP
      X=XLOW+K*XINC
      IF (XEND .EQ. ZRO) X=S(K+1)
      XX=X-XST+S(1)
      IF (K .LT. KAP) CALL XYZ (XX,YY,YYP,YYPP)
      IF (K .GE. KAP) YY=CMACH
      IF (K .GE. KAP) YYP=ZRO
      S(K+1)=X
      WMN(K+1)=YY
      TTR(K+1)=ONE+G8*YY**2
      SPR(K+1)=ONE/TTR(K+1)**(ONE+G1)
      DMDX(K+1)=YYP
23    DPX(K+1)=-GAM*YY*YYP*SPR(K+1)/TTR(K+1)
      S(1)=XST
      KAT=KUP+1
      KBL=KAT+4
      KO=1
      CALL BOUND
      IF (JB .EQ. 7) STOP
      IF (JB .GT. 10) GO TO 1
      WRITE (2,25) ITLE, SLONG
      WRITE (2,31) L1,L2,DC4
      WRITE (2,27) (S(K),RCO(K),DRX(K),K=1,KAT)
      GO TO 1
24    CLOSE(1)
      CLOSE(2)
      STOP
C
25    FORMAT (1H1,9X,3A4,'COORDINATES AND DERIVATIVES, LENGTH=',F12.7)
26    FORMAT (1H ,8X,2F15.6,1P4E20.8)
27    FORMAT (10(9X,0P2F15.6,1PE20.8/))
28    FORMAT (8E10.3)
29    FORMAT (1H )
30    FORMAT (3A4,I3)
31    FORMAT (1H0,14X,A4,'(IN)',7X,A4,'(IN)',6X,A8,12X,A8,14X,A8,9X,A8,2
     1X,A4 /)
      END 
